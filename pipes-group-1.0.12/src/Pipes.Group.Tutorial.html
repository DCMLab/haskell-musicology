<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd"><html xmlns="http://www.w3.org/1999/xhtml"><head><link rel="stylesheet" type="text/css" href="style.css" /><script type="text/javascript" src="highlight.js"></script></head><body><pre><span class="hs-pragma">{-# OPTIONS_GHC -fno-warn-unused-imports #-}</span><span>
</span><span id="line-2"></span><span>
</span><span id="line-3"></span><span class="hs-comment">{-| @pipes-group@ builds upon @pipes@ to establish idioms for grouping streams
    into sub-streams without collecting elements into memory.  This tutorial
    assumes familiarity with @pipes@ and @pipes-parse@.
-}</span><span>
</span><span id="line-7"></span><span>
</span><span id="line-8"></span><span class="hs-keyword">module</span><span> </span><span class="hs-identifier">Pipes.Group.Tutorial</span><span> </span><span class="hs-special">(</span><span>
</span><span id="line-9"></span><span>    </span><span class="annot"><span class="hs-comment">-- * Motivation</span></span><span>
</span><span id="line-10"></span><span>    </span><span class="annot"><span class="hs-comment">-- $motivation</span></span><span>
</span><span id="line-11"></span><span>
</span><span id="line-12"></span><span>    </span><span class="annot"><span class="hs-comment">-- * FreeT</span></span><span>
</span><span id="line-13"></span><span>    </span><span class="annot"><span class="hs-comment">-- $freeT</span></span><span>
</span><span id="line-14"></span><span>
</span><span id="line-15"></span><span>    </span><span class="annot"><span class="hs-comment">-- * How FreeT Works</span></span><span>
</span><span id="line-16"></span><span>    </span><span class="annot"><span class="hs-comment">-- $advanced</span></span><span>
</span><span id="line-17"></span><span>
</span><span id="line-18"></span><span>    </span><span class="annot"><span class="hs-comment">-- * Conclusion</span></span><span>
</span><span id="line-19"></span><span>    </span><span class="annot"><span class="hs-comment">-- $conclusion</span></span><span>
</span><span id="line-20"></span><span>    </span><span class="hs-special">)</span><span> </span><span class="hs-keyword">where</span><span>
</span><span id="line-21"></span><span>
</span><span id="line-22"></span><span class="hs-keyword">import</span><span> </span><span class="annot"><span class="hs-identifier">Pipes</span></span><span>
</span><span id="line-23"></span><span class="hs-keyword">import</span><span> </span><span class="annot"><a href="Pipes.Group.html"><span class="hs-identifier">Pipes.Group</span></a></span><span>
</span><span id="line-24"></span><span>
</span><span id="line-25"></span><span class="hs-comment">{- $motivation
    Dividing a stream into sub-streams is non-trivial.  To illustrate the
    problem, consider the following task: limit a stream to the first three
    groups of elements (a group means consecutive equal elements).

    The wrong way to do it is to read each group into memory like this:

&gt; import Lens.Family.State.Strict (zoom)
&gt; import Pipes
&gt; import Pipes.Parse
&gt; import qualified Pipes.Prelude as P
&gt; 
&gt; threeGroups :: (Monad m, Eq a) =&gt; Producer a m () -&gt; Producer a m ()
&gt; threeGroups p0 = loop 3 p0
&gt;   where
&gt;     loop 0 _ = return ()
&gt;     loop n p = do
&gt;         (as, p') &lt;- lift $ runStateT (zoom group drawAll) p
&gt;         each as
&gt;         loop (n - 1) p'

    The first problem is that this approach does not output any elements from
    each group until after parsing the entire group:

&gt;&gt;&gt; runEffect $ threeGroups P.stdinLn &gt;-&gt; P.stdoutLn
1&lt;Enter&gt;
1&lt;Enter&gt;
2&lt;Enter&gt;
1
1
2&lt;Enter&gt;
2&lt;Enter&gt;
3&lt;Enter&gt;
2
2
2
4&lt;Enter&gt;
3
&gt;&gt;&gt;

    Worse, this program will crash without outputting a single value if fed an
    infinitely long group of identical elements:

&gt;&gt;&gt; runEffect $ threeGroups (each (repeat 1)) &gt;-&gt; P.print
&lt;Consumes all memory and crashes&gt;

    A better approach is to just stream directly from the first three groups
    instead of storing the groups in intermediate lists:

&gt; import Lens.Family ((^.))
&gt; import Pipes
&gt; import Pipes.Parse
&gt; import qualified Pipes.Prelude as P
&gt; 
&gt; threeGroups :: (Monad m, Eq a) =&gt; Producer a m () -&gt; Producer a m ()
&gt; threeGroups p0 = loop 3 p0
&gt;   where
&gt;     loop 0 _ = return ()
&gt;     loop n p = do
&gt;         p' &lt;- p ^. group
&gt;         loop (n - 1) p'

    This will run in constant memory and stream values immediately:

&gt;&gt;&gt; runEffect $ threeGroups P.stdinLn &gt;-&gt; P.stdoutLn
1&lt;Enter&gt;
1
1&lt;Enter&gt;
1
2&lt;Enter&gt;
2
2&lt;Enter&gt;
2
2&lt;Enter&gt;
2
3&lt;Enter&gt;
3
4&lt;Enter&gt;

    However, this code is not very modular: we have to integrate our group
    creation logic with our group consumption logic.  This conflicts with the
    @pipes@ philosophy of decoupling streaming programs into modular components.

    An more modular approach would be to split our logic into three steps:

    * Split our 'Producer' into groups

    * Take the first three groups

    * Join these three groups back into a 'Producer'

    But how do we split our 'Producer' into groups without loading an entire
    group into memory?  We want to avoid solutions like the following code:

&gt; import Control.Monad (when, liftM2)
&gt; import Lens.Family.State.Strict (zoom)
&gt; import Pipes.Parse
&gt; 
&gt; split :: (Monad m, Eq a) =&gt; Producer a m () -&gt; Producer [a] m ()
&gt; split p = do
&gt;     ((as, eof), p') &lt;- lift (runStateT parser p)
&gt;     yield as
&gt;     when (not eof) (split p')
&gt;   where
&gt;     parser = liftM2 (,) (zoom group drawAll) isEndOfInput

    ... because then we're back where we started, loading entire groups into
    memory.
-}</span><span>
</span><span id="line-134"></span><span>
</span><span id="line-135"></span><span class="hs-comment">{- $freeT
    Fortunately, you can group elements while still streaming individual
    elements at a time.  The 'FreeT' type from the @free@ package solves this
    problem by allowing us to build \&quot;linked lists\&quot; of 'Producer's.  This lets
    you work with streams in a list-like manner.

    The key idea is that:

&gt; -- '~' means &quot;is analogous to&quot;
&gt;
&gt; -- If a Producer is like a list
&gt; Producer a m ()            ~   [a]
&gt;
&gt; -- ... then a 'FreeT'-delimited 'Producer' is like a list of lists
&gt; FreeT (Producer a m) m ()  ~  [[a]]

    Think of @(FreeT (Producer a m) m ())@ as a \&quot;list of 'Producer's\&quot;.
    'FreeT' nests each subsequent 'Producer' within the return value of the
    previous 'Producer' so that you cannot access the next 'Producer' until you
    completely drain the current 'Producer'.  However, you rarely need to work
    with 'FreeT' directly.  Instead, you can structure most things using
    \&quot;splitters\&quot;, \&quot;transformations\&quot; and \&quot;joiners\&quot;:

&gt; -- A &quot;splitter&quot;
&gt; Producer a m ()           -&gt; FreeT (Producer a m) m ()  ~   [a]  -&gt; [[a]]
&gt;
&gt; -- A &quot;transformation&quot;
&gt; FreeT (Producer a m) m () -&gt; FreeT (Producer a m) m ()  ~  [[a]] -&gt; [[a]]
&gt;
&gt; -- A &quot;joiner&quot;
&gt; FreeT (Producer a m) m () -&gt; Producer a m ()            ~  [[a]] -&gt;  [a]

    An example splitter is @(view groups)@, which splits a 'Producer' into
    'FreeT'-delimited 'Producer's, one for each group of consecutive equal
    elements:

&gt; view groups :: (Eq a, Monad m) =&gt; Producer a m x -&gt; FreeT (Producer a m) m x

    An example transformation is @(takes 3)@, which takes the first three
    'Producer's from a 'FreeT' and drops the rest:

&gt; takes 3 :: Monad m =&gt; FreeT (Producer a m) m () -&gt; FreeT (Producer a m) m ()

    An example joiner is @concats@, which collapses a 'FreeT' of 'Producer's
    back down into a single 'Producer':

&gt; concats :: Monad m =&gt; FreeT (Producer a m) m x -&gt; Producer a m x

    If you compose these three functions together, you will create a function
    that transforms a 'Producer' to keep only the first three groups of
    consecutive equal elements:

&gt; import Lens.Family
&gt; import Pipes
&gt; import Pipes.Group
&gt; import qualified Pipes.Prelude as P
&gt;
&gt; threeGroups :: (Monad m, Eq a) =&gt; Producer a m () -&gt; Producer a m ()
&gt; threeGroups = concats . takes 3 . view groups

    Both splitting and joining preserve the streaming nature of 'Producer's and
    do not collect or buffer any values.  The transformed 'Producer' still
    outputs values immediately and does not wait for groups to complete before
    producing results.

&gt;&gt;&gt; runEffect $ threeGroups P.stdinLn &gt;-&gt; P.stdoutLn
1&lt;Enter&gt;
1
1&lt;Enter&gt;
1
2&lt;Enter&gt;
2
2&lt;Enter&gt;
2
2&lt;Enter&gt;
2
3&lt;Enter&gt;
3
4&lt;Enter&gt;
&gt;&gt;&gt;

    Also, lenses simplify things even further.  The reason that 'groups' is a
    lens is because it actually combines both a splitter and joiner into a
    single package.  We can then use 'over' to handle both the splitting and
    joining for us:

&gt;&gt;&gt; runEffect $ over groups (takes 3) P.stdinLn &gt;-&gt; P.stdoutLn
&lt;Exact same behavior&gt;

    This behaves the same because 'over' takes care of calling the splitter
    before applying the transformation, then calling the inverse joiner
    afterward.

    Another useful lens is 'individually', which lets you apply transformations
    to each 'Producer' layer of a 'FreeT'.  For example, if we wanted to
    add an extra @&quot;!&quot;@ line to the end of every group, we would write:

&gt;&gt;&gt; import Control.Applicative ((&lt;*))
&gt;&gt;&gt; runEffect $ over (groups . individually) (&lt;* yield &quot;!&quot;) P.stdinLn &gt;-&gt; P.stdoutLn
1&lt;Enter&gt;
1
1&lt;Enter&gt;
1
2&lt;Enter&gt;
!
2
2&lt;Enter&gt;
2
2&lt;Enter&gt;
2
3&lt;Enter&gt;
!
3
4&lt;Enter&gt;
!
&gt;&gt;&gt;

    Note that 'individually' is only compatible with the @lens@ package.  You
    can alternatively use 'maps' if you are using @lens-family-core@:

&gt;&gt;&gt; runEffect $ over groups (maps (&lt;* yield &quot;!&quot;)) P.stdinLn &gt;-&gt; P.stdoutLn
&lt;Exact same behavior&gt;

-}</span><span>
</span><span id="line-259"></span><span>
</span><span id="line-260"></span><span class="hs-comment">{- $advanced
    You don't necessarily have to restrict yourself to predefined 'FreeT'
    functions.  You can also manually build or recurse over 'FreeT's of
    'Producer's.

    For example, here is how 'concats' is implemented, which collapses all the
    'Producer's within a 'FreeT' into a single 'Producer':

&gt; concats :: Monad m =&gt; FreeT (Producer a m) m x -&gt; Producer a m x
&gt; concats = go
&gt;   where
&gt;     go f = do
&gt;         x &lt;- lift (runFreeT f)  -- Match against the &quot;head&quot; of the &quot;list&quot;
&gt;         case x of
&gt;             Pure r -&gt; return r  -- The &quot;list&quot; is empty
&gt;             Free p -&gt; do        -- The &quot;list&quot; is non-empty
&gt;                 f' &lt;- p         -- The return value of the 'Producer' is
&gt;                 go f'           --     the &quot;tail&quot; of the &quot;list&quot;

    Many patterns for 'FreeT's have equivalent analogs for lists.  'runFreeT'
    behaves like pattern matching on the list, except that you have to bind the
    result.  'Pure' is analogous to @[]@ and 'Free' is analogous to @(:)@.

    When you receive a 'Free' constructor that means you have a 'Producer' whose
    return value is the rest of the list (i.e. another 'FreeT').  You cannot
    access the rest of the list without running the 'Producer' to completion to
    retrieve this return value.  The above example just runs the entire
    'Producer', binds the remainder of the list to @f'@ and then recurses on
    that value.

    You can also build 'FreeT's in a manner similar to lists.  For example, the
    'chunksOf' lens uses the following splitter function internally:

&gt; _chunksOf :: Monad m =&gt; Producer a m x -&gt; FreeT (Producer a m) m x
&gt; _chunksOf p = FreeT $ do
&gt;     x &lt;- next p                     -- Pattern match on the 'Producer'
&gt;     return $ case x of
&gt;         Left   r      -&gt; Pure r     -- Build an empty &quot;list&quot;
&gt;         Right (a, p') -&gt; Free $ do  -- Build a non-empty &quot;list&quot;
&gt;             p'' &lt;- (yield a &gt;&gt; p')^.splitAt n0  -- Emit the &quot;head&quot;
&gt;             return (_chunksOf p'')              -- Return the &quot;tail&quot;

    'Pure' signifies an empty 'FreeT' (one with no 'Producer' layers), just like
    @[]@ signifies an empty list (one with no elements).  We return 'Pure'
    whenever we cannot emit any more 'Producer's.

    'Free' indicates that we wish to emit a 'Producer' followed by another
    \&quot;list\&quot;.  The 'Producer' we run directly within the body of the 'Free'.
    However, we store the remainder of the \&quot;list\&quot; within the return value of
    the 'Producer'.  This is where @_chunksOf@ recurses to build the rest of the
    \&quot;list\&quot;.

    To gain a better understanding for how 'FreeT' works, consult the definition
    of the type, which you can find in &quot;Control.Monad.Trans.Free&quot;:

&gt; newtype FreeT f m a = FreeT { runFreeT :: m (FreeF f a (FreeT f m a)) }
&gt;
&gt; data FreeF f a b = Pure a | Free (f b)

    ... and just replace all occurrences of @f@ with @(Producer e m)@:

&gt; -- This is pseudocode
&gt;
&gt; newtype FreeT' m a = FreeT { runFreeT :: m (FreeF' a (FreeT' m a)) }
&gt;
&gt; data FreeF' a b = Pure a | Free (Producer e m b)

    ... which you can further think of as:

&gt; -- More pseudocode
&gt;
&gt; newtype FreeT' m a =
&gt;     FreeT { runFreeT :: m (Pure a | Producer e m (FreeT' m a)) }

    In other words, 'runFreeT' unwraps a 'FreeT' to produce an action in the
    base monad which either finishes with a value of type @a@ or continues with
    a 'Producer' which returns a new 'FreeT'.  Vice versa, if you want to build
    a 'FreeT', you must create an action in the base monad which returns either
    a 'Pure' or a 'Producer' wrapping another 'FreeT'.
-}</span><span>
</span><span id="line-340"></span><span>
</span><span id="line-341"></span><span class="hs-comment">{- $conclusion
    This library is very small since it only contains element-agnostic grouping
    utilities.  Downstream libraries that provide richer grouping utilities
    include @pipes-bytestring@ and @pipes-text@.

    To learn more about @pipes-group@, ask questions, or follow development, you
    can subscribe to the @haskell-pipes@ mailing list at:

    &lt;https://groups.google.com/forum/#!forum/haskell-pipes&gt;

    ... or you can mail the list directly at:

    &lt;mailto:haskell-pipes@googlegroups.com&gt;
-}</span><span>
</span><span id="line-355"></span></pre></body></html>